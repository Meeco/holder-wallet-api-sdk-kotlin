plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.8.10'
    id 'maven-publish'
    id 'com.diffplug.spotless' version '6.13.0'
    id 'cl.franciscosolis.sonatype-central-upload' version '1.0.0'
}

group 'me.meeco'
version project.VERSION_NUMBER


wrapper {
    gradleVersion = '7.5'
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

repositories {
    maven { url "https://repo1.maven.org/maven2" }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

sonatypeCentralUpload {
    username = project.SONATYPE_USERNAME
    password = project.SONATYPE_PASSWORD
    
    archives = files("./build/libs/holder-wallet-api-kotlin-sdk-$version\.jar", "./build/libs/holder-wallet-api-kotlin-sdk-$version-javadoc.jar", "./build/libs/holder-wallet-api-kotlin-sdk-$version-sources.jar")
    pom = file("./build/publications/mavenJava/pom-default.xml")
    
    signingKey = project.SIGNING_KEY
    signingKeyPassphrase = project.SIGNING_KEY_PASSPHRASE
    publicKey = project.PUBLIC_KEY
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'holder-wallet-api-kotlin-sdk'
                description = 'holder-wallet-api-kotlin-sdk'
                url = 'https://github.com/Meeco/holder-wallet-api-sdk-kotlin'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'meeco'
                        name = 'meeco'
                        email = 'support@meeco.me'
                    }
                }
                scm {
                    url = 'https://github.com/Meeco/holder-wallet-api-sdk-kotlin'
                }
            }
        }
    }
}


spotless {
    enforceCheck false

    format 'misc', {
        target '.gitignore'

        trimTrailingWhitespace()
        indentWithSpaces()
        endWithNewline()
    }
    kotlin {
        ktfmt()
    }
}

test {
    useJUnitPlatform()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlin:kotlin-reflect"
    implementation "com.squareup.moshi:moshi-kotlin:1.14.0"
    implementation "com.squareup.moshi:moshi-adapters:1.14.0"
    implementation "com.squareup.okhttp3:okhttp:4.11.0"
    testImplementation "io.kotlintest:kotlintest-runner-junit5:3.4.2"
}